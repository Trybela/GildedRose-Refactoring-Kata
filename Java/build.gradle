plugins {
    id 'java'
    id 'jacoco'
    id 'info.solidsoft.pitest' version '1.15.0'
    id 'io.freefair.lombok' version "8.13.1"
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.2')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly  'org.junit.jupiter:junit-jupiter-engine'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    pitest 'org.pitest:pitest-junit5-plugin:1.2.1'
}

group = 'com.gildedrose'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

test {
    useJUnitPlatform()
}

task texttest(type: JavaExec) {
    mainClass.set('com.gildedrose.TexttestFixture')
    classpath = sourceSets.test.runtimeClasspath
    args "30"
}

jacocoTestReport {
    executionData(test)
    reports {
        html.required = true
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
}

jacocoTestCoverageVerification {
    executionData(test)
    violationRules {
        rule {
            includes = ['com/gildedrose/lifecycle/**']
            limit {
                minimum = 1
            }
        }
    }
}

pitest {
    junit5PluginVersion = '1.2.1'
    targetClasses = ['com.gildedrose.lifecycle.*']
    targetTests = ['com.gildedrose.*']
    mutators = ['STRONGER']
    threads = 4
    outputFormats = ['HTML']
    timestampedReports = false

}

